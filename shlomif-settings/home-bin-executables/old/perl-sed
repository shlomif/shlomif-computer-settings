#!/usr/bin/env perl

use strict;
use warnings;

use IO::All;

my $sep_re = '[=-]+[\]>]?';

my %transformations = ();

my @arguments = @ARGV;
my %excludes;
while ( defined( my $arg = shift(@arguments) ) )
{
    if ( $arg eq "--exclude" )
    {
        my $param = shift(@arguments);
        $excludes{$param} = 1;
    }
    elsif ( $arg =~ /^(?:\w+$sep_re)+\w+$/ )
    {
        my @words = ( split /$sep_re/, $arg );
        $transformations{ $words[-2] } = $words[-1];
    }
    else
    {
        die "Incorrect Argument \"$arg\"!";
    }
}

sub dump_transforms
{
    print +( map { "$_ ==> " . $transformations{$_} . "\n" }
            keys(%transformations) );
}

sub trans
{
    my $identifier = shift;
    if ( exists( $transformations{$identifier} ) )
    {
        return $transformations{$identifier};
    }
    else
    {
        return $identifier;
    }
}

# dump_transforms();

my @filenames = io->dir(".")->all;

FILE_LOOP: foreach my $filename ( grep { /\.[ch]\z/ } @filenames )
{
    if ( exists( $excludes{$filename} ) )
    {
        next FILE_LOOP;
    }
    my $text     = io->file($filename)->slurp;
    my $new_text = $text;
    $new_text =~ s/\b(\w+)\b/trans($1)/ge;
    if ( $new_text ne $text )
    {
        io->file($filename)->print($new_text);
    }
}

=head1 COPYRIGHT & LICENSE

Copyright 2017 by Shlomi Fish

This program is distributed under the MIT / Expat License:
L<http://www.opensource.org/licenses/mit-license.php>

Permission is hereby granted, free of charge, to any person
obtaining a copy of this software and associated documentation
files (the "Software"), to deal in the Software without
restriction, including without limitation the rights to use,
copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following
conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.

=cut
