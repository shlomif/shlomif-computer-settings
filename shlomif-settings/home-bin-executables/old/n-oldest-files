#!/usr/bin/env perl

use strict;
use warnings;

use Heap::Elem::Ref    (qw(RefElem));
use Heap::Binary       ();
use File::Find::Object ();
use Getopt::Long qw/ GetOptions /;

package Elem;

sub new
{
    my $class = shift;
    my $self  = shift;
    return bless $self, $class;
}

sub cmp
{
    my $self  = shift;
    my $other = shift;

    return (   ( -( $self->{mtime} <=> $other->{mtime} ) )
            || ( $self->{fn} cmp $other->{fn} ) );
}

package main;

my $num_to_find = 1_000;
my $output_nul  = 0;

GetOptions(
    '--limit=i' => \$num_to_find,
    '-0'        => \$output_nul,
);

my $dir_to_scan = shift(@ARGV);

my $ffo        = File::Find::Object->new( {}, $dir_to_scan );
my $files_heap = Heap::Binary->new();
my $heap_count = 0;

while ( my $result = $ffo->next_obj() )
{
    $files_heap->add(
        RefElem(
            Elem->new(
                {
                    mtime => $result->stat_ret->[9],
                    fn    => $result->path(),
                }
            )
        )
    );

    if ( ++$heap_count > $num_to_find )
    {
        $files_heap->extract_top();
        --$heap_count;
    }
}

while ( my $elem = $files_heap->extract_top() )
{
    print $elem->val->{fn}, ( $output_nul ? "\0" : "\n" );
}

=head1 COPYRIGHT & LICENSE

Copyright 2017 by Shlomi Fish

This program is distributed under the MIT / Expat License:
L<http://www.opensource.org/licenses/mit-license.php>

Permission is hereby granted, free of charge, to any person
obtaining a copy of this software and associated documentation
files (the "Software"), to deal in the Software without
restriction, including without limitation the rights to use,
copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following
conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.

=cut
