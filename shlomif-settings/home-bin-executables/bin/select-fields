#!/usr/bin/perl

# Written by Shlomi Fish - http://www.shlomifish.org/ - 2016
# Licensed under the MIT/Expat License.
#
# Example:
# cat my.tsv | select-fields -f Time -f Iterations -f Delta

use strict;
use warnings;

use Getopt::Long qw/ GetOptions /;
use List::MoreUtils qw/firstidx/;

my @fields;
GetOptions( 'fields|f=s' => \@fields )
    or die('Error in command line arguments.');

my $header = <>;
chomp $header;

my @h = split /\t/, $header;
my @indices;
for my $f (@fields)
{
    my $i = firstidx { $_ eq $f } @h;
    if ( $i < 0 )
    {
        die "Could not find field $f in header!";
    }
    push @indices, $i;
}

sub print_l
{
    my ($l) = @_;

    chomp $l;
    print join( "\t", ( split /\t/, $l )[@indices] ), "\n";

    return;
}

print_l($header);
while ( my $l = <> )
{
    print_l($l);
}
