#!/usr/bin/perl

# This program attempts to upgrade a CPAN changes from my own peculiar
# format to something that https://metacpan.org/release/CPAN-Changes likes.

use strict;
use warnings;

use DateTime::Format::Strptime;

my $strp = DateTime::Format::Strptime->new(
    pattern   => '%a %d %b %T %Y',
    locale    => 'en_US',
    time_zone => 'Asia/Jerusalem',
);

my $strp_us = DateTime::Format::Strptime->new(
    pattern   => '%a %b %d %T %Y',
    locale    => 'en_US',
    time_zone => 'Asia/Jerusalem',
);

while (my $l = <ARGV>)
{
    chomp($l);
    if (my ($v, $space, $date_str) = $l =~ m{\A(v?[\d\.]+)(\s+)(\S.*)\z}ms)
    {
        $date_str =~ s/\s+\b(?:IDT|IST)\b\s+/ /;
        if (defined (my $d = $strp->parse_datetime($date_str) // $strp_us->parse_datetime($date_str)))
        {
            print $v, $space, $d->strftime("%Y-%m-%d"), "\n";
        }
        else
        {
            die "Could not parse Line '$l'";
        }
    }
    else
    {
        print "$l\n";
    }
}

__END__

=head1 COPYRIGHT & LICENSE

Copyright 2013 by Shlomi Fish

This program is distributed under the MIT (X11) License:
L<http://www.opensource.org/licenses/mit-license.php>

Permission is hereby granted, free of charge, to any person
obtaining a copy of this software and associated documentation
files (the "Software"), to deal in the Software without
restriction, including without limitation the rights to use,
copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following
conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.

=cut
