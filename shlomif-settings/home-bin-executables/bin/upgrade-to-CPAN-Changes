#!/usr/bin/perl

# This program attempts to upgrade a CPAN changes from my own peculiar
# format to something that https://metacpan.org/release/CPAN-Changes likes.

use strict;
use warnings;

use List::Util qw(first);

use DateTime::Format::Strptime;

my @strp_s = (map {
    DateTime::Format::Strptime->new(
    pattern   => $_,
    locale    => 'en_US',
    time_zone => 'Asia/Jerusalem',
    )
    }
    (
        '%b %d, %Y',
        '%Y-%m-%d',
        '%a %b %d %T %Y',
        '%a %d %b %T %Y',
        '%a %d %b %Y',
        '%a %b %d %Y',
    )
);

while (my $l = <STDIN>)
{
    chomp($l);
    if ($l !~ /Unknown/ and my ($v, $space, $date_str, $author) = $l =~ m{\A(v?[\d\.]+)(\s+)(?:-\s*)?(\S[^\-]*?)((?: - .*)?)\z}ms)
    {
        $date_str =~ s/\s+\b(?:IDT|IST)\b\s+/ /;
        if (defined (my $d = first {defined($_) } map { $_->parse_datetime($date_str) } @strp_s))
        {
            printf ("%-8s%s%s\n", $v, $d->strftime("%Y-%m-%d"), $author);
        }
        else
        {
            die "Could not parse Line '$l'";
        }
    }
    else
    {
        print "$l\n";
    }
}

__END__

=head1 COPYRIGHT & LICENSE

Copyright 2013 by Shlomi Fish

This program is distributed under the MIT (X11) License:
L<http://www.opensource.org/licenses/mit-license.php>

Permission is hereby granted, free of charge, to any person
obtaining a copy of this software and associated documentation
files (the "Software"), to deal in the Software without
restriction, including without limitation the rights to use,
copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following
conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.

=cut
